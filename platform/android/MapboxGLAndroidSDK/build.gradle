apply plugin: 'com.android.library'

dependencies {
    implementation "com.android.support:support-annotations:${supportLibVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"

    implementation rootProject.ext.dep.timber
    implementation rootProject.ext.dep.okhttp3
    implementation rootProject.ext.dep.lost

    // Mapbox Android Services (GeoJSON support)
    implementation(rootProject.ext.dep.mapboxJavaGeoJSON) {
        transitive = true
    }

    // Mapbox Android Services (Telemetry support)
    implementation(rootProject.ext.dep.mapboxAndroidTelemetry) {
        transitive = true
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion.toInteger()

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion.toInteger()
        targetSdkVersion rootProject.ext.targetSdkVersion.toInteger()
        buildConfigField "String", "GIT_REVISION_SHORT", String.format("\"%s\"", getGitRevision())
        buildConfigField "String", "MAPBOX_VERSION_STRING", String.format("\"Mapbox/%s\"", project.VERSION_NAME)
        buildConfigField "String", "MAPBOX_EVENTS_USER_AGENT", String.format("\"MapboxEventsAndroid/%s\"", project.VERSION_NAME)
    }

    // avoid naming conflicts, force usage of prefix
    resourcePrefix 'mapbox_'

    sourceSets {
        // limit amount of exposed library resources
        main.res.srcDirs += 'src/main/res-public'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
    }

    buildTypes {
        debug {
            jniDebuggable true
        }

        release {
            // aar proguard configuration
            consumerProguardFiles 'proguard-rules.pro'
            jniDebuggable false
        }
    }
    packagingOptions {
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
        exclude 'lib/mips/libmapbox-gl.so'
        exclude 'lib/mips/libsqlcipher.so'
    }
}

def getGitRevision() {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    def ref = proc.text.trim()
    return ref
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'commons-collections', module: 'commons-collections'
}

//apply from: 'gradle-javadoc.gradle'
apply from: 'gradle-publish.gradle'
apply from: 'gradle-checkstyle.gradle'
